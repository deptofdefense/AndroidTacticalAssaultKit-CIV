
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('net.sf.proguard:proguard-gradle') with module('com.guardsquare:proguard-gradle:7.1.1')
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { subProject ->
    if(subProject.path.startsWith(':takkernel:')) {
        // more robust dependency resolution based on simple project name for when
        // `takkernel` is included in multi-project configuration (e.g. integrated
        // into the ATAK development environment)
        subProject.dependencies.ext.project = { p ->
            return subProject.parent.findProject(p) ?:
                    subProject.parent.findProject(":${parent.name}:${p}")
        }
    }
}

