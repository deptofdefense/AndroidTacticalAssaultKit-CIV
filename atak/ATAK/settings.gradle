
static def getValueFromPropertiesFile(File propFile, String key) {
    if(!propFile.isFile() || !propFile.canRead())
        return null
    def prop = new Properties()
    def reader = propFile.newReader()
    try {
        prop.load(reader)
    } finally {
        reader.close()
    }
    return prop.get(key)
}

include ':app'

// if `takkernel` is present, include projects directly
if((getValueFromPropertiesFile(file('local.properties'), 'takkernel.dependency.forceRemote') != 'true') &&
        new File('../../takkernel').exists()) {

    includeFlat('takkernel')
    project(':takkernel').projectDir = new File(settingsDir, '../../takkernel')
    include(':takkernel:annotation')
    include(':takkernel:engine')
    include(':takkernel:port')
    include(':takkernel:shared')
}

