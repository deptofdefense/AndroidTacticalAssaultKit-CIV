if (isJavaKernelBuild) {
    apply plugin: 'java-library' // apply the Java plugin

    sourceSets {
        main {
            // add default location for java specific sourceset
            java.srcDirs 'src/jre/java'
        }
    }

    test {
        useJUnit()
        maxHeapSize = '1G'
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.java.srcDirs
    }
} else if (isAndroidKernelBuild) {
    apply plugin: 'com.android.library' // apply the Android plugin

    // basic android library configuration
    android {
        compileSdkVersion project.androidCompileSdkVersion
        buildToolsVersion project.androidBuildToolsVersion

        buildTypes {
            debug {}
            release {}
        }

        defaultConfig {
            targetSdkVersion project.androidTargetSdkVersion
            compileSdkVersion project.androidCompileSdkVersion
            minSdkVersion project.androidMinSdkVersion
            ndk {
                ndkVersion "${androidNdkVersion}"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }

            versionCode project.versionCode
            versionName "${project.version}"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        // create a default manifest
        def manifestFile = project.file('src/android/AndroidManifest.xml')

        if (!manifestFile.exists()) {
            manifestFile.getParentFile().mkdirs()
            manifestFile.text = """
<manifest xmlns:android="http://schemas.android.com/apk/res/android"
          package="gov.tak.kernel.${project.name}" >
</manifest>
"""
        }

        // note that `sourceSets` closure is _within_ the `android` closure for Android builds
        sourceSets {
            main {
                // specify the manifest
                manifest.srcFile 'src/android/AndroidManifest.xml'
                // add default location for android specific sourceset
                java.srcDirs 'src/android/java'
                // add default location for android JNI libs (shared across all build types)
                jniLibs.srcDirs 'src/android/jniLibs'
            }
            test {
                java.srcDirs 'src/main/test'
            }
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }
}

// Customize our archive basename, which defines the resulting publication artifactId
project.archivesBaseName = "${rootProject.name}-${project.name}"

dependencies {
    // Use JUnit test framework
    testImplementation("junit:junit:${junitVersion}")
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

// configure the build dir for the kernel build target (e.g. `build/android` or `build/java`)
project.buildDir = new File(buildDir, kernelBuildTarget)
