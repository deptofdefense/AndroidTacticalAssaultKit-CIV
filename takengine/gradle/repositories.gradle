ext.getValueFromPropertiesFile = { propFile, key ->
    if(!propFile.isFile() || !propFile.canRead())
        return null
    def prop = new Properties()
    def reader = propFile.newReader()
    try {
        prop.load(reader)
    } finally {
        reader.close()
    }
    return prop.get(key)
}

/**
 * Returns the value of the named property if it is defined, else the specified
 * default. Project properties are always evaluated first, followed by
 * `local.properties` defined in the project root.
 */
ext.getProperty = { project, name, defValue ->
    def prop = project.properties[name] ?:
            getValueFromPropertiesFile(project.rootProject.file('local.properties'), name)
    return (null == prop) ? defValue : prop
}

// determine the kernel build type
def takrepoUrl = getProperty(project, 'takrepo.url', 'http://localhost')
def takrepoUser = getProperty(project, 'takrepo.user', 'invalid')
def takrepoPassword = getProperty(project, 'takrepo.password', 'invalid')

repositories {
    google()
    mavenLocal()
    mavenCentral()
    maven {
        url = takrepoUrl
        credentials {
            username takrepoUser
            password takrepoPassword
        }
    }
}

ext.repos = repositories
