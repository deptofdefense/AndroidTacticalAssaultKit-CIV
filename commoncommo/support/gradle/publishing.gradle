apply from: 'gradle/versions.gradle'

def publishGroup = 'jcommoncommo'

publishing {
    repositories {
        maven {
            name = 'upstream'
            credentials {
                username System.getenv('ARTIFACTORY_USER')
                password System.getenv('ARTIFACTORY_PASS')
            }
            url = System.getenv('ARTIFACTORY_MAVEN_URL')
        }
    }

    publications {
        // DEPRECATED: remove at version 2.0.0
        jcommoncommoNative(MavenPublication) {
            group = "$publishGroup"
            artifactId = 'jcommoncommo-native'
            version = "${commoncommoVersion}"
            artifact "$buildDir/jcommoncommo-native.zip"
        }

        jcommoncommo(MavenPublication) {
            group = "$publishGroup"
            artifactId = 'jcommoncommo'
            version = "${commoncommoVersion}"
            artifact "$rootDir/../jcommoncommo.jar"
            // define dependency on `jcommoncommo-rt`
            pom.withXml {
                def dependencies  = asNode().appendNode("dependencies")
                def depNode  = dependencies.appendNode("dependency")
                depNode.appendNode('groupId', "$publishGroup")
                depNode.appendNode('artifactId', 'jcommoncommo-rt')
                depNode.appendNode('version', "${commoncommoVersion}")
            }
        }

        jcommoncommoRuntime(MavenPublication) {
            group = "$publishGroup"
            artifactId = 'jcommoncommo-rt'
            version = "${commoncommoVersion}"
            artifact "$buildDir/jcommoncommo-rt.jar"
        }
    }
}
